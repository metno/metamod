<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:wms="http://www.opengis.net/wms"
  xmlns:xlink="http://www.w3.org/1999/xlink"
>

  <xsl:output indent="yes"/>
  <xsl:param name="crs" select="'EPSG:32761'"/>
  <xsl:param name="units" select="'m'"/>
  <xsl:param name="left"/>
  <xsl:param name="right"/>
  <xsl:param name="bottom"/>
  <xsl:param name="top"/>

  <xsl:template match="/">

    <ViewContext version="1.1.0" id="OpenLayers_Context_466"
      xmlns="http://www.opengis.net/context"
      xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
      xmlns:default="http://www.opengis.net/context"
      xmlns:xlink="http://www.w3.org/1999/xlink"
      xmlns:ol="http://openlayers.org/context"
      xsi:schemaLocation="http://www.opengis.net/context http://schemas.opengis.net/context/1.1.0/context.xsd">
      <xsl:comment>Generated by METAMOD from WMS GetCapabilities</xsl:comment>
      <General>
        <!--Window width="798" height="573"/-->
        <xsl:apply-templates select="//wms:Layer/wms:BoundingBox[1]"/>
        <BoundingBox minx="{$left}" miny="{$bottom}" maxx="{$right}" maxy="{$top}" SRS="{$crs}"/>
        <Title>WMS viewer</Title>
        <Extension>
          <ol:maxExtent minx="{$left}" miny="{$bottom}" maxx="{$right}" maxy="{$top}"/>
          <ol:units><xsl:value-of select="$units"/></ol:units>
        </Extension>
      </General>
      <LayerList>
        <xsl:apply-templates select="//wms:Layer[wms:Name]"/>
      </LayerList>
    </ViewContext>

  </xsl:template>


  <xsl:template match="//wms:Layer">

    <Layer xmlns="http://www.opengis.net/context" queryable="0" hidden="0">
      <Server service="OGC:WMS" version="1.1.1">
        <!-- FIXME: change namespace from wms to wmc -->
        <xsl:copy-of select="/*/wms:Capability/wms:Request/wms:GetMap/wms:DCPType/wms:HTTP/wms:Get/wms:OnlineResource"/>
      </Server>

      <Name><xsl:value-of select="wms:Name"/></Name>
      <Title><xsl:value-of select="wms:Title"/></Title>
      <Abstract><xsl:value-of select="wms:Abstract"/></Abstract>
      <FormatList>
        <xsl:for-each select="/*/wms:Capability/wms:Request/wms:GetMap/wms:Format">
          <Format><xsl:value-of select="."/></Format>
        </xsl:for-each>
      </FormatList>
      <StyleList>
        <xsl:apply-templates select="./wms:Style"/>
      </StyleList>
    </Layer>

  </xsl:template>

  <xsl:template match="//wms:Layer/wms:Style">
    <xsl:copy-of select="."/> <!-- FIXME: change namespace from wms to wmc -->
  </xsl:template>

</xsl:stylesheet>
