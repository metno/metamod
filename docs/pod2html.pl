#!/usr/bin/perl -w

=begin LICENCE

----------------------------------------------------------------------------
  METAMOD - Web portal for metadata search and upload

  Copyright (C) 2013 met.no

  Contact information:
  Norwegian Meteorological Institute
  Box 43 Blindern
  0313 OSLO
  NORWAY
  email: geir.aalberg@met.no

  This file is part of METAMOD

  METAMOD is free software; you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation; either version 2 of the License, or
  (at your option) any later version.

  METAMOD is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
  GNU General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with METAMOD; if not, write to the Free Software
  Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
----------------------------------------------------------------------------

=end LICENCE

=cut

use strict;
use warnings;
use File::Spec;
use Data::Dumper;
use Pod::Simple 3.30;

=head1 NAME

B<pod2html.pl> - generate HTML from POD

=head1 DESCRIPTION

Extension of the CPAN Pod::Simple tool to accomodate various types of links in
the METAMOD documentation. Normally invoked only via C<make docs> or F<makedocs.sh>.

=head1 USAGE

Although intended as a utility for other scripts, you can call this script manually:

    $ ./pod2html.pl [-v] <podfile>

This is normally only sensible during development, as the output files in ./html
will be overwritten next time makedocs.sh is run.

=head2 A note on METAMOD API docs

These are not generated by pod2html.pl, but via running C<make apidocs>.

=cut

my $verbose = 0;
my $pod = Metamod::Pod::Simple::HTML->new;
$pod->index(1);

$Data::Dumper::Indent = 0;
$Data::Dumper::Terse  = 1;

foreach (@ARGV) {
    if (/^-v$/) {
        $verbose = 1;
        next;
    }

    print STDERR "Parsing file $_:\n" if $verbose;

    # compute relative path to top dir
    my ($volume, $directories, $file) = File::Spec->splitpath( $_ );
    $directories =~ s|/$||;
    my @dirs = File::Spec->splitdir( $directories );
    my $topdirpath = File::Spec->catdir( map { '..' if $_ } @dirs );
    #printf STDERR "  Paths: %s\n", Dumper [ $directories, $topdirpath, \@dirs ] if $verbose;
    $topdirpath .= '/' if $topdirpath;

    # add html
    $pod->html_css( "${topdirpath}mmdocs.css?view=co" );
    $pod->top_anchor("<div id='banner'>METAMOD Documentation | "
                     . "<a name='___top' href='${topdirpath}index.html'>main menu</a>"
                     . ($topdirpath ? " | <a href='index.html'>$directories</a>" : '')
                     ."</div>");

    $pod->parse_from_file($_);
}


#######################################
# move below to lib later
# also add Pod::Simple 3.30 to cpanfile

package Metamod::Pod::Simple::HTML v2.0.1;
use base qw( Pod::Simple::HTML  );

use Data::Dumper;
#$Data::Dumper::Deparse = 1;
$Data::Dumper::Useqq=1;

=head1 Link specification

The following kinds of links are recognized:

=over 4

=item Internal anchors

  L<"The Metadata Store">

=item POD files in same directory (with title)

  L<Configuration|configuration>

=item POD files in other directories

  L<Upgrading|upgrade/index>
  L<Top index|../index>

=item source files outside docs

  L<"debianE<sol>control"|debian/control>

=item API modules

  L<Metamod::Dataset>

=back

=cut

sub do_pod_link {
    my ( $self, $token ) = @_;
    #printf STDERR "  Self: %s\n", Dumper($self);
    my $link = $token->attr('to');
    my $linkname = $$link[2];
    my $type = $token->attr('type');
    my $section = $token->attr('section');
    my $sectionname = $$section[2];

    if (! $linkname) {
        # internal anchor links
        die "Unknown link type " . $token->dump unless $sectionname;
        printf STDERR "### %s\n", $token->dump if $verbose;
        $sectionname =~ s/ /_/g;
        return $self->{base}||'' . "#$sectionname";
        # TODO - make anchors for head2 [done?]
    }

    if ($sectionname) {
        if ($linkname =~ /^\.\./ or -d $linkname or $linkname eq 'api') {
            # link to POD in parent or child dir
            # relative link to file outside docs in source tree
            printf STDERR "--- %s\n", $token->dump if $verbose;
            return "./$linkname/$sectionname.html?view=co";
        } else {
            # relative from source root instead of docs/html.. e.g. "debian/control"
            printf STDERR ">>> %s\n", $token->dump if $verbose;
            return "../../$linkname/$sectionname?view=co";
         }
    } else {
        if ($linkname =~ /::/) {
            # presumably API doc
            printf STDERR "@@@ %s\n", $token->dump if $verbose;
            $linkname =~ s|::|/|g;
            return "./api/$linkname.html?view=co";
        } else {
            # link to POD in same dir
            printf STDERR "??? %s\n", $token->dump if $verbose;
            return "./$linkname.html?view=co";
        }
    }
}

1;

=head1 AUTHOR

Geir Aalberg, E<lt>geira@met.noE<gt>

=head1 LICENSE

Copyright (C) 2015 The Norwegian Meteorological Institute.

METAMOD is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 2 of the License, or
(at your option) any later version.

=cut
