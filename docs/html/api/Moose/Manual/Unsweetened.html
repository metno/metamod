<html><head><title>Moose::Manual::Unsweetened</title>
<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1" >
<link rel="stylesheet" title="blkbluw" type="text/css" href="../../_blkbluw.css" media="all" >
<link rel="alternate stylesheet" title="blkmagw" type="text/css" href="../../_blkmagw.css" media="all" >
<link rel="alternate stylesheet" title="blkcynw" type="text/css" href="../../_blkcynw.css" media="all" >
<link rel="alternate stylesheet" title="whtprpk" type="text/css" href="../../_whtprpk.css" media="all" >
<link rel="alternate stylesheet" title="whtnavk" type="text/css" href="../../_whtnavk.css" media="all" >
<link rel="alternate stylesheet" title="grygrnk" type="text/css" href="../../_grygrnk.css" media="all" >
<link rel="alternate stylesheet" title="whtgrng" type="text/css" href="../../_whtgrng.css" media="all" >
<link rel="alternate stylesheet" title="blkgrng" type="text/css" href="../../_blkgrng.css" media="all" >
<link rel="alternate stylesheet" title="grygrnw" type="text/css" href="../../_grygrnw.css" media="all" >
<link rel="alternate stylesheet" title="blkbluw" type="text/css" href="../../_blkbluw.css" media="all" >
<link rel="alternate stylesheet" title="whtpurk" type="text/css" href="../../_whtpurk.css" media="all" >
<link rel="alternate stylesheet" title="whtgrng" type="text/css" href="../../_whtgrng.css" media="all" >
<link rel="alternate stylesheet" title="grygrnw" type="text/css" href="../../_grygrnw.css" media="all" >

<script type="text/javascript" src="../../_podly.js"></script>

</head>
<body class='pod'>
<!--
  generated by Pod::Simple::HTML v3.16,
  using Pod::Simple::PullParser v3.16,
  under Perl v5.014002 at Tue Mar 25 17:14:21 2014 GMT.

 If you want to change this HTML document, you probably shouldn't do that
   by changing it directly.  Instead, see about changing the calling options
   to Pod::Simple::HTML, and/or subclassing Pod::Simple::HTML,
   then reconverting this document from the Pod source.
   When in doubt, email the author of Pod::Simple::HTML for advice.
   See 'perldoc Pod::Simple::HTML' for more info.

-->

<!-- start doc -->
<p class="backlinktop"><b><a name="___top" href="../../index.html" accesskey="1" title="All Documents">&lt;&lt;</a></b></p>

<div class='indexgroup'>
<ul   class='indexList indexList1'>
  <li class='indexItem indexItem1'><a href='#NAME'>NAME</a>
  <li class='indexItem indexItem1'><a href='#VERSION'>VERSION</a>
  <li class='indexItem indexItem1'><a href='#DESCRIPTION'>DESCRIPTION</a>
  <li class='indexItem indexItem1'><a href='#CLASSES_AND_ATTRIBUTES'>CLASSES AND ATTRIBUTES</a>
  <li class='indexItem indexItem1'><a href='#AUTHOR'>AUTHOR</a>
  <li class='indexItem indexItem1'><a href='#COPYRIGHT_AND_LICENSE'>COPYRIGHT AND LICENSE</a>
</ul>
</div>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="NAME"
>NAME</a></h1>

<p>Moose::Manual::Unsweetened - Moose idioms in plain old Perl 5 without the sugar</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="VERSION"
>VERSION</a></h1>

<p>version 2.0604</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="DESCRIPTION"
>DESCRIPTION</a></h1>

<p>If you&#39;re trying to figure out just what the heck Moose does,
and how it saves you time,
you might find it helpful to see what Moose is <i>really</i> doing for you.
This document shows you the translation from Moose sugar back to plain old Perl 5.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="CLASSES_AND_ATTRIBUTES"
>CLASSES AND ATTRIBUTES</a></h1>

<p>First,
we define two very small classes the Moose way.</p>

<pre>  package Person;

  use DateTime;
  use DateTime::Format::Natural;
  use Moose;
  use Moose::Util::TypeConstraints;

  has name =&#62; (
      is       =&#62; &#39;rw&#39;,
      isa      =&#62; &#39;Str&#39;,
      required =&#62; 1,
  );

  # Moose doesn&#39;t know about non-Moose-based classes.
  class_type &#39;DateTime&#39;;

  my $en_parser = DateTime::Format::Natural-&#62;new(
      lang      =&#62; &#39;en&#39;,
      time_zone =&#62; &#39;UTC&#39;,
  );

  coerce &#39;DateTime&#39;
      =&#62; from &#39;Str&#39;
      =&#62; via { $en_parser-&#62;parse_datetime($_) };

  has birth_date =&#62; (
      is      =&#62; &#39;rw&#39;,
      isa     =&#62; &#39;DateTime&#39;,
      coerce  =&#62; 1,
      handles =&#62; { birth_year =&#62; &#39;year&#39; },
  );

  enum &#39;ShirtSize&#39; =&#62; qw( s m l xl xxl );

  has shirt_size =&#62; (
      is      =&#62; &#39;rw&#39;,
      isa     =&#62; &#39;ShirtSize&#39;,
      default =&#62; &#39;l&#39;,
  );</pre>

<p>This is a fairly simple class with three attributes. We also define an enum type to validate t-shirt sizes because we don&#39;t want to end up with something like &#34;blue&#34; for the shirt size!</p>

<pre>  package User;

  use Email::Valid;
  use Moose;
  use Moose::Util::TypeConstraints;

  extends &#39;Person&#39;;

  subtype &#39;Email&#39;
      =&#62; as &#39;Str&#39;
      =&#62; where { Email::Valid-&#62;address($_) }
      =&#62; message { &#34;$_ is not a valid email address&#34; };

  has email_address =&#62; (
      is       =&#62; &#39;rw&#39;,
      isa      =&#62; &#39;Email&#39;,
      required =&#62; 1,
  );</pre>

<p>This class subclasses Person to add a single attribute, email address.</p>

<p>Now we will show what these classes would look like in plain old Perl 5. For the sake of argument, we won&#39;t use any base classes or any helpers like <code>Class::Accessor</code>.</p>

<pre>  package Person;

  use strict;
  use warnings;

  use Carp qw( confess );
  use DateTime;
  use DateTime::Format::Natural;

  sub new {
      my $class = shift;
      my %p = ref $_[0] ? %{ $_[0] } : @_;

      exists $p{name}
          or confess &#39;name is a required attribute&#39;;
      $class-&#62;_validate_name( $p{name} );

      exists $p{birth_date}
          or confess &#39;birth_date is a required attribute&#39;;

      $p{birth_date} = $class-&#62;_coerce_birth_date( $p{birth_date} );
      $class-&#62;_validate_birth_date( $p{birth_date} );

      $p{shirt_size} = &#39;l&#39;
          unless exists $p{shirt_size}:

      $class-&#62;_validate_shirt_size( $p{shirt_size} );

      return bless \%p, $class;
  }

  sub _validate_name {
      shift;
      my $name = shift;

      local $Carp::CarpLevel = $Carp::CarpLevel + 1;

      defined $name
          or confess &#39;name must be a string&#39;;
  }

  {
      my $en_parser = DateTime::Format::Natural-&#62;new(
          lang      =&#62; &#39;en&#39;,
          time_zone =&#62; &#39;UTC&#39;,
      );

      sub _coerce_birth_date {
          shift;
          my $date = shift;

          return $date unless defined $date &#38;&#38; ! ref $date;

          my $dt = $en_parser-&#62;parse_datetime($date);

          return $dt ? $dt : undef;
      }
  }

  sub _validate_birth_date {
      shift;
      my $birth_date = shift;

      local $Carp::CarpLevel = $Carp::CarpLevel + 1;

      $birth_date-&#62;isa(&#39;DateTime&#39;)
          or confess &#39;birth_date must be a DateTime object&#39;;
  }

  sub _validate_shirt_size {
      shift;
      my $shirt_size = shift;

      local $Carp::CarpLevel = $Carp::CarpLevel + 1;

      defined $shirt_size
          or confess &#39;shirt_size cannot be undef&#39;;

      my %sizes = map { $_ =&#62; 1 } qw( s m l xl xxl );

      $sizes{$shirt_size}
          or confess &#34;$shirt_size is not a valid shirt size (s, m, l, xl, xxl)&#34;;
  }

  sub name {
      my $self = shift;

      if (@_) {
          $self-&#62;_validate_name( $_[0] );
          $self-&#62;{name} = $_[0];
      }

      return $self-&#62;{name};
  }

  sub birth_date {
      my $self = shift;

      if (@_) {
          my $date = $self-&#62;_coerce_birth_date( $_[0] );
          $self-&#62;_validate_birth_date( $date );

          $self-&#62;{birth_date} = $date;
      }

      return $self-&#62;{birth_date};
  }

  sub birth_year {
      my $self = shift;

      return $self-&#62;birth_date-&#62;year;
  }

  sub shirt_size {
      my $self = shift;

      if (@_) {
          $self-&#62;_validate_shirt_size( $_[0] );
          $self-&#62;{shirt_size} = $_[0];
      }

      return $self-&#62;{shirt_size};
  }</pre>

<p>Wow, that was a mouthful! One thing to note is just how much space the data validation code consumes. As a result, it&#39;s pretty common for Perl 5 programmers to just not bother. Unfortunately, not validating arguments leads to surprises down the line (&#34;why is birth_date an email address?&#34;).</p>

<p>Also, did you spot the (intentional) bug?</p>

<p>It&#39;s in the <code>_validate_birth_date()</code> method. We should check that the value in <code>$birth_date</code> is actually defined and an object before we go and call <code>isa()</code> on it! Leaving out those checks means our data validation code could actually cause our program to die. Oops.</p>

<p>Note that if we add a superclass to Person we&#39;ll have to change the constructor to account for that.</p>

<p>(As an aside, getting all the little details of what Moose does for you just right in this example was really not easy, which emphasizes the point of the example. Moose saves you a lot of work!)</p>

<p>Now let&#39;s see User:</p>

<pre>  package User;

  use strict;
  use warnings;

  use Carp qw( confess );
  use Email::Valid;
  use Scalar::Util qw( blessed );

  use base &#39;Person&#39;;

  sub new {
      my $class = shift;
      my %p = ref $_[0] ? %{ $_[0] } : @_;

      exists $p{email_address}
          or confess &#39;email_address is a required attribute&#39;;
      $class-&#62;_validate_email_address( $p{email_address} );

      my $self = $class-&#62;SUPER::new(%p);

      $self-&#62;{email_address} = $p{email_address};

      return $self;
  }

  sub _validate_email_address {
      shift;
      my $email_address = shift;

      local $Carp::CarpLevel = $Carp::CarpLevel + 1;

      defined $email_address
          or confess &#39;email_address must be a string&#39;;

      Email::Valid-&#62;address($email_address)
          or confess &#34;$email_address is not a valid email address&#34;;
  }

  sub email_address {
      my $self = shift;

      if (@_) {
          $self-&#62;_validate_email_address( $_[0] );
          $self-&#62;{email_address} = $_[0];
      }

      return $self-&#62;{email_address};
  }</pre>

<p>That one was shorter, but it only has one attribute.</p>

<p>Between the two classes, we have a whole lot of code that doesn&#39;t do much. We could probably simplify this by defining some sort of &#34;attribute and validation&#34; hash, like this:</p>

<pre>  package Person;

  my %Attr = (
      name =&#62; {
          required =&#62; 1,
          validate =&#62; sub { defined $_ },
      },
      birth_date =&#62; {
          required =&#62; 1,
          validate =&#62; sub { blessed $_ &#38;&#38; $_-&#62;isa(&#39;DateTime&#39;) },
      },
      shirt_size =&#62; {
          required =&#62; 1,
          validate =&#62; sub { defined $_ &#38;&#38; $_ =~ /^(?:s|m|l|xl|xxl)$/i },
      }
  );</pre>

<p>Then we could define a base class that would accept such a definition, and do the right thing. Keep that sort of thing up and we&#39;re well on our way to writing a half-assed version of Moose!</p>

<p>Of course, there are CPAN modules that do some of what Moose does, like <code>Class::Accessor</code>, <code>Class::Meta</code>, and so on. But none of them put together all of Moose&#39;s features along with a layer of declarative sugar, nor are these other modules designed for extensibility in the same way as Moose. With Moose, it&#39;s easy to write a MooseX module to replace or extend a piece of built-in functionality.</p>

<p>Moose is a complete OO package in and of itself, and is part of a rich ecosystem of extensions. It also has an enthusiastic community of users, and is being actively maintained and developed.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="AUTHOR"
>AUTHOR</a></h1>

<p>Moose is maintained by the Moose Cabal, along with the help of many contributors. See <a href="../../Moose.html#CABAL" class="podlinkpod"
>&#34;CABAL&#34; in Moose</a> and <a href="../../Moose.html#CONTRIBUTORS" class="podlinkpod"
>&#34;CONTRIBUTORS&#34; in Moose</a> for details.</p>

<h1><a class='u' href='#___top' title='click to go to top of document'
name="COPYRIGHT_AND_LICENSE"
>COPYRIGHT AND LICENSE</a></h1>

<p>This software is copyright (c) 2012 by Infinity Interactive, Inc..</p>

<p>This is free software; you can redistribute it and/or modify it under the same terms as the Perl 5 programming language system itself.</p>
<p class="backlinkbottom"><b><a name="___bottom" href="../../index.html" title="All Documents">&lt;&lt;</a></b></p>

<!-- end doc -->

</body></html>
