<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns="http://www.opengis.net/context"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:ol="http://openlayers.org/context"
>

  <!-- stylesheet for transforming WMS 1.1.1 (only) to WMC 1.1.0 -->

  <xsl:output indent="yes"/>
  <xsl:param name="crs" select="'EPSG:4326'"/> <!-- default proj is lat/lon -->
  <xsl:param name="units" select="'m'"/>
  <xsl:param name="left"/>
  <xsl:param name="right"/>
  <xsl:param name="bottom"/>
  <xsl:param name="top"/>
  <xsl:param name="time"/>

  <xsl:template match="/">

    <ViewContext version="1.1.0" id="OpenLayers_Context_466"
      xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
      xmlns:default="http://www.opengis.net/context"
      xsi:schemaLocation="http://www.opengis.net/context http://schemas.opengis.net/context/1.1.0/context.xsd">
      <xsl:comment>Generated by METAMOD from WMS GetCapabilities on <xsl:value-of select="$time"/></xsl:comment>
      <General>
        <BoundingBox minx="{$left}" miny="{$bottom}" maxx="{$right}" maxy="{$top}" SRS="{$crs}"/>
        <Title>WMS viewer</Title>
        <Extension>
          <ol:maxExtent minx="{$left}" miny="{$bottom}" maxx="{$right}" maxy="{$top}"/>
          <!--<ol:maxExtent minx="-60" miny="{$bottom}" maxx="{$right}" maxy="{$top}"/>-->
          <ol:units><xsl:value-of select="$units"/></ol:units>
        </Extension>
      </General>
      <LayerList>
        <xsl:apply-templates select="//Layer[not(child::Layer)]"/>
      </LayerList>
    </ViewContext>

  </xsl:template>


  <xsl:template match="//Layer">

    <Layer queryable="{@queryable}" hidden="1">
      <Server service="OGC:WMS" version="1.1.1">
        <OnlineResource xlink:type="simple"
            xlink:href="{/*/Capability/Request/GetMap/DCPType/HTTP/Get/OnlineResource/@xlink:href}"/>
      </Server>

      <Name><xsl:value-of select="Name"/></Name>
      <Title><xsl:value-of select="Title"/></Title>
      <Abstract><xsl:value-of select="Abstract"/></Abstract>

      <xsl:for-each select="ancestor::Layer/SRS">
        <!-- TODO: check rules if defined on multiple layers... sum or replacement? FIXME -->
        <SRS><xsl:value-of select="."/></SRS>
      </xsl:for-each>

      <DimensionList>
        <xsl:apply-templates select="Dimension"/>
      </DimensionList>

      <FormatList>
        <xsl:for-each select="/*/Capability/Request/GetMap/Format">
          <Format>
            <xsl:if test="starts-with(text(), 'image/png')">
              <xsl:attribute name="current">1</xsl:attribute>
            </xsl:if>
            <xsl:value-of select="."/>
          </Format>
        </xsl:for-each>
      </FormatList>
      <StyleList>
        <xsl:apply-templates select="./Style"/>
      </StyleList>
    </Layer>

  </xsl:template>

  <xsl:template match="//Layer/Dimension"> <!-- maybe add default time? -->
    <xsl:element name="Dimension">
      <xsl:for-each select="@*">
        <xsl:attribute name="{local-name()}">
          <xsl:value-of select="."/>
        </xsl:attribute>
      </xsl:for-each>
      <xsl:value-of select="normalize-space(.)"/>
    </xsl:element>
  </xsl:template>

  <!-- next 4 templates only used for transforming style nodes -->

  <xsl:template match="*">
    <xsl:element name="{local-name()}" namespace="http://www.opengis.net/context">
      <xsl:apply-templates select="@* | node()"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="@*[namespace-uri() = '']">
    <!-- strip namespace from native WMS attributes -->
    <xsl:attribute name="{local-name()}">
      <xsl:value-of select="."/>
    </xsl:attribute>
  </xsl:template>

  <xsl:template match="@*[namespace-uri() != '']">
    <!-- should only match xlinks in WMS document -->
    <xsl:attribute name="{name()}">
      <xsl:value-of select="."/>
    </xsl:attribute>
  </xsl:template>

  <xsl:template match="text() | comment() | processing-instruction()">
    <xsl:copy/>
  </xsl:template>

</xsl:stylesheet>
