<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns="http://www.opengis.net/context"
  xmlns:wms="http://www.opengis.net/wms"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:ol="http://openlayers.org/context"
  xmlns:setup="http://www.met.no/schema/metamod/ncWmsSetup"
  xmlns:date="http://exslt.org/dates-and-times"

>

  <!--stylesheet for transforming ncWmsSetup documents to WMC 1.1.0-->
  <!--supports both WMS 1.1.1 and 1.3.0-->
  <!--does not currently handle wildcards in layer names in ncWmsSetup-->

  <xsl:output indent="yes"/>

  <xsl:template match="/">
    <ViewContext version="1.1.0" id="OpenLayers_Context_466"
      xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
      xmlns:default="http://www.opengis.net/context"
      xsi:schemaLocation="http://www.opengis.net/context http://schemas.opengis.net/context/1.1.0/context.xsd">
      <xsl:comment>Generated by METAMOD from WMS GetCapabilities on <xsl:value-of select="date:date-time()"/></xsl:comment>
      <xsl:apply-templates select="/*/setup:displayArea"/>
      <LayerList>
        <xsl:apply-templates select="/*/setup:baselayer"/>
        <xsl:apply-templates select="/*/setup:layer"/>
      </LayerList>
    </ViewContext>
  </xsl:template>

  <xsl:template match="/*/setup:displayArea">
    <General>
      <BoundingBox minx="{@left}" miny="{@bottom}" maxx="{@right}" maxy="{@top}" SRS="{@crs}"/>
      <Title>WMS viewer</Title>
      <Extension>
        <ol:maxExtent minx="{@left}" miny="{@bottom}" maxx="{@right}" maxy="{@top}"/>
        <ol:units><xsl:value-of select="m"/></ol:units> <!--FIXME-->
      </Extension>
    </General>
  </xsl:template>

  <xsl:template match="/*/setup:layer|/*/setup:baselayer">
    <xsl:variable name="name" select="@name"/>
    <xsl:variable name="proj" select="/*/setup:displayArea/@crs"/>
    <xsl:variable name="url" select="concat(@url, '?service=WMS&amp;request=GetCapabilities&amp;version=1.3.0')"/>
    <xsl:variable name="capab" select="document($url)"/>
    <xsl:variable name="version" select="$capab/*/@version"/>
    <xsl:comment><xsl:value-of select="concat($name,' - wms ',$version)"/></xsl:comment>
    <xsl:variable name="foo" select="$capab/*/wms:Capability/wms:Layer"/>
    <xsl:variable name="layer" select="$capab/descendant::wms:Layer[wms:Name=$name]"/>
    <!--check which WMS version the server actually returned-->
    <xsl:choose>
      <xsl:when test="$version = '1.3.0'">
       <xsl:apply-templates select="$capab/descendant::wms:Layer[wms:Name=$name]"/> <!--and ancestor-or-self::wms:CRS=$proj-->
      </xsl:when>
      <xsl:when test="$version = '1.1.1'">
        <xsl:apply-templates select="$capab/descendant::Layer[Name=$name]"/> <!--and ancestor-or-self::SRS=$proj-->
      </xsl:when>
    </xsl:choose>
  </xsl:template>

  <xsl:template match="//wms:Layer"> <!--Cap v 1.3.1-->
    <Layer queryable="{@queryable}" hidden="1">
      <Server service="OGC:WMS" version="1.1.1">
        <OnlineResource xlink:type="simple"
            xlink:href="{/*/wms:Capability/wms:Request/wms:GetMap/wms:DCPType/wms:HTTP/wms:Get/wms:OnlineResource/@xlink:href}"/>
      </Server>
      <Name><xsl:value-of select="wms:Name"/></Name>
      <Title><xsl:value-of select="wms:Title"/></Title>
      <Abstract><xsl:value-of select="wms:Abstract"/></Abstract>

      <xsl:for-each select="ancestor-or-self::wms:Layer/wms:CRS">
        <!-- TODO: check rules if defined on multiple layers... sum or replacement? FIXME -->
        <SRS><xsl:value-of select="."/></SRS>
      </xsl:for-each>

      <DimensionList>
        <xsl:apply-templates select="wms:Dimension"/>
      </DimensionList>

      <FormatList>
        <xsl:for-each select="/*/wms:Capability/wms:Request/wms:GetMap/wms:Format">
          <Format>
            <xsl:if test="text() = 'image/png'">
              <xsl:attribute name="current">1</xsl:attribute>
            </xsl:if>
            <xsl:value-of select="."/>
          </Format>
        </xsl:for-each>
      </FormatList>

      <StyleList>
        <xsl:apply-templates select="./wms:Style"/> <!--TODO set current=1-->
      </StyleList>

      <ol:transparent>true</ol:transparent>
      <ol:opacity>0.6</ol:opacity>

    </Layer>
  </xsl:template>

  <xsl:template match="//Layer"> <!--Cap v 1.1.1-->
    <Layer queryable="{@queryable}" hidden="1">
      <Server service="OGC:WMS" version="1.1.1">
        <OnlineResource xlink:type="simple"
            xlink:href="{/*/Capability/Request/GetMap/DCPType/HTTP/Get/OnlineResource/@xlink:href}"/>
      </Server>
      <Name><xsl:value-of select="Name"/></Name>
      <Title><xsl:value-of select="Title"/></Title>
      <Abstract><xsl:value-of select="Abstract"/></Abstract>

      <xsl:for-each select="ancestor-or-self::Layer/SRS">
        <!-- TODO: check rules if defined on multiple layers... sum or replacement? FIXME -->
        <SRS><xsl:value-of select="."/></SRS>
      </xsl:for-each>

      <DimensionList>
        <xsl:apply-templates select="Extent"/>
      </DimensionList>

      <FormatList> <!--merge with above-->
        <xsl:for-each select="/*/Capability/Request/GetMap/Format">
          <Format>
            <xsl:if test="text() = 'image/png'">
              <xsl:attribute name="current">1</xsl:attribute>
            </xsl:if>
            <xsl:value-of select="."/>
          </Format>
        </xsl:for-each>
      </FormatList>

      <StyleList>
        <xsl:apply-templates select="./Style"/> <!--TODO set current=1-->
      </StyleList>

      <ol:transparent>false</ol:transparent>

    </Layer>
  </xsl:template>

  <xsl:template match="wms:Dimension|Extent"> <!-- maybe add default time? -->
    <xsl:element name="Dimension">
      <xsl:for-each select="@*">
        <xsl:attribute name="{local-name()}">
          <xsl:value-of select="."/>
        </xsl:attribute>
      </xsl:for-each>
      <xsl:attribute name="current">
        <xsl:value-of select="1"/> <!-- if dimensions given, they must be used in request -->
      </xsl:attribute>
      <xsl:value-of select="normalize-space(.)"/>
    </xsl:element>
  </xsl:template>

  <!-- next 4 templates only used for transforming style nodes -->

  <xsl:template match="*">
    <xsl:element name="{local-name()}" namespace="http://www.opengis.net/context">
      <xsl:apply-templates select="@* | node()"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="@*[namespace-uri() = 'http://www.opengis.net/wms']">
    <!-- strip namespace from native WMS attributes -->
    <xsl:attribute name="{local-name()}">
      <xsl:value-of select="."/>
    </xsl:attribute>
  </xsl:template>

  <xsl:template match="@*[namespace-uri() != 'http://www.opengis.net/wms']">
    <!-- should only match xlinks in WMS document -->
    <xsl:attribute name="{name()}">
      <xsl:value-of select="."/>
    </xsl:attribute>
  </xsl:template>

  <xsl:template match="text() | comment() | processing-instruction()">
    <xsl:copy/>
  </xsl:template>

</xsl:stylesheet>
