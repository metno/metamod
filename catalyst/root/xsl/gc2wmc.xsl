<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns="http://www.opengis.net/context"
  xmlns:wms="http://www.opengis.net/wms"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:ol="http://openlayers.org/context"
>

  <xsl:output indent="yes"/>
  <xsl:param name="crs" select="'EPSG:4326'"/> <!-- default proj is lat/lon -->
  <xsl:param name="units" select="'m'"/>
  <xsl:param name="left"/>
  <xsl:param name="right"/>
  <xsl:param name="bottom"/>
  <xsl:param name="top"/>
  <xsl:param name="time"/>

  <xsl:template match="/">

    <ViewContext version="1.1.0" id="OpenLayers_Context_466"
      xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
      xmlns:default="http://www.opengis.net/context"
      xsi:schemaLocation="http://www.opengis.net/context http://schemas.opengis.net/context/1.1.0/context.xsd">
      <xsl:comment>Generated by METAMOD from WMS GetCapabilities on <xsl:value-of select="$time"/></xsl:comment>
      <General>
        <BoundingBox minx="{$left}" miny="{$bottom}" maxx="{$right}" maxy="{$top}" SRS="{$crs}"/>
        <Title>WMS viewer</Title>
        <Extension>
          <ol:maxExtent minx="{$left}" miny="{$bottom}" maxx="{$right}" maxy="{$top}"/>
          <!--<ol:maxExtent minx="-60" miny="{$bottom}" maxx="{$right}" maxy="{$top}"/>-->
          <ol:units><xsl:value-of select="$units"/></ol:units>
        </Extension>
      </General>
      <LayerList>
        <xsl:apply-templates select="//wms:Layer[not(child::wms:Layer)]"/>
      </LayerList>
    </ViewContext>

  </xsl:template>


  <xsl:template match="//wms:Layer">

    <Layer queryable="{@queryable}" hidden="1">
      <Server service="OGC:WMS" version="1.1.1">
        <OnlineResource xlink:type="simple"
            xlink:href="{/*/wms:Capability/wms:Request/wms:GetMap/wms:DCPType/wms:HTTP/wms:Get/wms:OnlineResource/@xlink:href}"/>
      </Server>

      <Name><xsl:value-of select="wms:Name"/></Name>
      <Title><xsl:value-of select="wms:Title"/></Title>
      <Abstract><xsl:value-of select="wms:Abstract"/></Abstract>

      <DimensionList>
        <xsl:apply-templates select="wms:Dimension"/>
      </DimensionList>

      <FormatList>
        <xsl:for-each select="/*/wms:Capability/wms:Request/wms:GetMap/wms:Format">
          <Format>
            <xsl:if test="text() = 'image/png'">
              <xsl:attribute name="current">1</xsl:attribute>
            </xsl:if>
            <xsl:value-of select="."/>
          </Format>
        </xsl:for-each>
      </FormatList>
      <StyleList>
        <xsl:apply-templates select="./wms:Style"/>
      </StyleList>
    </Layer>

  </xsl:template>

  <xsl:template match="//wms:Layer/wms:Dimension"> <!-- maybe add default time? -->
    <xsl:element name="Dimension">
      <xsl:for-each select="@*">
        <xsl:attribute name="{local-name()}">
          <xsl:value-of select="."/>
        </xsl:attribute>
      </xsl:for-each>
      <xsl:value-of select="normalize-space(.)"/>
    </xsl:element>
  </xsl:template>

  <!-- next 4 templates only used for transforming style nodes -->

  <xsl:template match="*">
    <xsl:element name="{local-name()}" namespace="http://www.opengis.net/context">
      <xsl:apply-templates select="@* | node()"/>
    </xsl:element>
  </xsl:template>

  <xsl:template match="@*[namespace-uri() = 'http://www.opengis.net/wms']">
    <!-- strip namespace from native WMS attributes -->
    <xsl:attribute name="{local-name()}">
      <xsl:value-of select="."/>
    </xsl:attribute>
  </xsl:template>

  <xsl:template match="@*[namespace-uri() != 'http://www.opengis.net/wms']">
    <!-- should only match xlinks in WMS document -->
    <xsl:attribute name="{name()}">
      <xsl:value-of select="."/>
    </xsl:attribute>
  </xsl:template>

  <xsl:template match="text() | comment() | processing-instruction()">
    <xsl:copy/>
  </xsl:template>

</xsl:stylesheet>
